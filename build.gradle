group 'EUTS'
version '1.0'

apply plugin: 'java'
apply plugin: 'idea'

repositories {
    mavenCentral()
}

configurations {
    download
    testDownload
}

dependencies {
    download(
            'org.scream3r:jssc:2.8.0',
            'junit:junit:4.11+',
            'com.google.guava:guava:11.0.2',
            'com.h2database:h2:1.4.191',
            'com.github.lgooddatepicker:LGoodDatePicker:4.3.1',
            'com.healthmarketscience.sqlbuilder:sqlbuilder:2.1.2'
    )
    testDownload(
            'org.mockito:mockito-core:1.10.+'
    )

    compile fileTree(dir: 'libs/compile', include: '**/*.jar')
    testCompile fileTree(dir: 'libs/testCompile', include: '**/*.jar')
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = false
    }
}

//create a single Jar with all dependencies
task createExecutiveJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Gradle Jar File Example',
                'Implementation-Version': version,
                'Main-Class': 'EUTS'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task cleanDownloadedDependencies(type: Delete) {
    delete fileTree('libs/compile/downloaded')
}

task cleanDownloadedTestDependencies(type: Delete) {
    delete fileTree('libs/testCompile/downloaded')
}

task downloadDependencies(type: Copy) {
    from configurations.download
    into "libs/compile/downloaded/"
}

task downloadTestDependencies(type: Copy) {
    from configurations.testDownload
    into "libs/testCompile/downloaded/"
}

task updateDependencies {
    dependsOn   cleanDownloadedDependencies,
            cleanDownloadedTestDependencies,
            downloadDependencies,
            downloadTestDependencies
}

